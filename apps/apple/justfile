set export

APP_NAME := "Base64"
SCHEME := APP_NAME
PROJECT := APP_NAME + ".xcodeproj"
OUTPUT := "out"
ARCHIVE_PATH := OUTPUT / APP_NAME + ".xcarchive"
MACOS_DESTINATION := "generic/platform=macOS"
MACOS_SDK := "macosx"
APP_PATH := APP_NAME + ".app"
DMG_NAME := APP_NAME + ".dmg"
DMG_RESOURCES := "DMGResources"
DMG_BACKGROUND := DMG_RESOURCES / "background.png"
DMG_ICON := APP_PATH / "Contents/Resources/AppIconMacOS.icns"

archive-mac: install-xcpretty prepare-archive
    #!/bin/zsh

    export CONFIG="Release"

    set -o pipefail && xcodebuild archive \
        -project "$PROJECT" \
        -scheme "$SCHEME" \
        -configuration "$CONFIG" \
        -archivePath "$ARCHIVE_PATH" \
        -sdk "$MACOS_SDK" \
        -destination "$MACOS_DESTINATION" | xcpretty

create-dmg: install-create-dmg
    #!/bin/zsh

    if [ -f $DMG_NAME ]
    then
        echo "Removing existing DMG file $DMG_NAME..."
        rm $DMG_NAME
    fi

    if [ ! -d $APP_PATH ]
    then
        echo "Error: $APP_PATH does not exist. Please build the app first."
        exit 1
    fi

    if [ ! -f $DMG_BACKGROUND ]
    then
        echo "Error: Background image $DMG_BACKGROUND does not exist."
        exit 1
    fi

    if [ ! -f $DMG_ICON ]
    then
        echo "Error: Icon file $DMG_ICON does not exist."
        exit 1
    fi

    echo "Creating DMG for $APP_NAME..."
    create-dmg \
        --volname $APP_NAME \
        --volicon $DMG_ICON \
        --background $DMG_BACKGROUND \
        --window-pos 200 120 \
        --window-size 800 500 \
        --icon-size 128 \
        --icon "$APP_NAME.app" 200 250 \
        --app-drop-link 600 250 \
        --no-internet-enable \
        --skip-jenkins \
        $DMG_NAME \
        $APP_PATH

    if [ $? -eq 0 ]
    then
        echo "DMG created successfully: $APP_NAME.dmg"
    else
        echo "Error creating DMG"
        exit 1
    fi

    echo "Done creating $APP_NAME DMG!"

bootstrap: install-tools

[private]
install-tools: install-create-dmg install-xcpretty

[private]
install-xcpretty:
    #!/bin/zsh

    if ! command -v xcpretty &> /dev/null
    then
        echo "xcpretty is not installed. Installing via Gem..."

        gem install create-dmg || exit 1
    fi

[private]
install-create-dmg:
    #!/bin/zsh

    if ! command -v create-dmg &> /dev/null
    then
        echo "create-dmg is not installed. Installing via Homebrew..."
        if ! command -v brew &> /dev/null
        then
            echo "Homebrew is not installed. Please install Homebrew first: https://brew.sh/"
            exit 1
        fi

        brew install create-dmg
    fi

[private]
prepare-archive:
    #!/bin/zsh

    mkdir -p $OUTPUT
